#!/usr/bin/env python

Import("env")

if env["platform"] in ["windows"] and env["use_angle"]:
    # ANGLE on windows desktop
    thirdparty_dir = "#thirdparty/angle/include"
    env.Prepend(CPPPATH=[thirdparty_dir])
    env.Append(LIBPATH=["#thirdparty/angle/out/Release"])
elif env["platform"] in ["windows", "haiku", "osx", "x11"]:
    # Thirdparty source files
    thirdparty_dir = "#thirdparty/glad/"
    thirdparty_sources = [
        "glad.c",
    ]
    thirdparty_sources = [thirdparty_dir + file for file in thirdparty_sources]

    env.Prepend(CPPPATH=[thirdparty_dir])

    env.Append(CPPDEFINES=["GLAD_ENABLED"])
    env.Append(CPPDEFINES=["GLES_OVER_GL"])

    env_thirdparty = env.Clone()
    env_thirdparty.disable_warnings()
    env_thirdparty.add_source_files(env.drivers_sources, thirdparty_sources)

# Godot source files
env.add_source_files(env.drivers_sources, "*.cpp")
